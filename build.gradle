plugins {
	id 'org.springframework.boot' version '2.4.4'
	id 'io.spring.dependency-management' version '1.0.11.RELEASE'
	id 'java'
	id 'idea'
	id 'jacoco'
}

group = 'net.pet.project'
description = 'Simple rest Application'
version = '0.0.1'
compileJava.options.encoding = "UTF-8"
compileTestJava.options.encoding = "UTF-8"
java.sourceCompatibility = JavaVersion.VERSION_15
java.targetCompatibility = JavaVersion.VERSION_15
jar.enabled = true

import org.apache.tools.ant.filters.ReplaceTokens
processResources {
	filesMatching("**/application.yml") {
		filter ReplaceTokens, tokens: [
				'project.description' : project.description,
				'project.version' : project.version
		]
	}
}

configurations {
	compileOnly {
		extendsFrom annotationProcessor
	}
}

repositories {
	mavenLocal()
	mavenCentral()
}

ext {
	set('swaggerVersion', "1.5.6")
	set('testContainersVersion', "1.15.2")
}

dependencies {
	/*SPRING STARTER*/
	implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
	implementation 'org.springframework.boot:spring-boot-starter-validation'
	implementation 'org.springframework.boot:spring-boot-starter-web'

	/*LOMBOK*/
	compileOnly 'org.projectlombok:lombok'
	annotationProcessor 'org.projectlombok:lombok'
	testCompileOnly 'org.projectlombok:lombok'
	testAnnotationProcessor 'org.projectlombok:lombok'
	testCompileOnly 'org.projectlombok:lombok'

	/*SWAGGER*/
	implementation "org.springdoc:springdoc-openapi-ui:${swaggerVersion}"
	implementation "org.springdoc:springdoc-openapi-data-rest:${swaggerVersion}"

	/*UTIL*/
	implementation 'org.jetbrains:annotations:16.0.2'
	implementation 'org.apache.commons:commons-lang3'

	/*STORAGE*/
	runtimeOnly 'org.postgresql:postgresql'

	/*TEST*/
	testImplementation 'org.springframework.boot:spring-boot-starter-test'
	testCompile "org.testcontainers:testcontainers:${testContainersVersion}"
	testCompile "org.testcontainers:junit-jupiter:${testContainersVersion}"
	testCompile "org.testcontainers:postgresql:${testContainersVersion}"
	developmentOnly 'org.springframework.boot:spring-boot-devtools'
}

jacocoTestReport {
	reports {
		xml.enabled true
		csv.enabled false
		html.enabled true
	}
	afterEvaluate {}
}

test {
	failFast = false

	testLogging {
		exceptionFormat "FULL"
		events "PASSED", "FAILED", "SKIPPED", "STANDARD_OUT"
		displayGranularity = 4
	}
	useJUnitPlatform {
		includeEngines 'junit-jupiter'
	}
	afterSuite { desc, result ->
		if (!desc.parent) { // will match the outermost suite
			def output = "Results: ${result.resultType} (${result.testCount} tests, ${result.successfulTestCount} successes, ${result.failedTestCount} failures, ${result.skippedTestCount} skipped)"
			def startItem = '|  ', endItem = '  |'
			def repeatLength = startItem.length() + output.length() + endItem.length()
			println('\n' + ('-' * repeatLength) + '\n' + startItem + output + endItem + '\n' + ('-' * repeatLength))
		}
	}
}
test.finalizedBy jacocoTestReport

task jpaTest {
	description 'Run jpa-test'

	doLast {
		println 'Start jpa-test'
		test.filter {
			includeTestsMatching "net.pet.project.simplerest.module.jpa.*"
		}
	}
}
jpaTest.finalizedBy test

task mvcTest {
	description 'Run mvc-test'

	doLast {
		println 'Start mvc-test'
		test.filter {
			includeTestsMatching "net.pet.project.simplerest.module.mvc.*"
		}
	}
}
mvcTest.finalizedBy test

task unitTest {
	description 'Run unit-test'

	doLast {
		println 'Start unit-test'
		test.filter {
			includeTestsMatching "net.pet.project.simplerest.unit.*"
		}
	}
}
unitTest.finalizedBy test